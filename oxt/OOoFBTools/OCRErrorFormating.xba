<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="OCRErrorFormating" script:language="StarBasic">&apos;======================================================================================================================
&apos; 		модуль обработки ошибок OCR форматирования: снятие курсива и/или bold со знаков препинания, кавычек и скобок
&apos;======================================================================================================================

Option Explicit

Public oOCREF As Object
Private Const sOCREFSettings As String = &quot;ocr_error_formating.txt&quot; &apos; файл настроек
Private Iter As Integer

&apos;******************************************************************
&apos; инициализация и запуск диалога исправления OCR ошибок форматирования
Sub OCRErrorFormatStart()
&apos;	DialogLibraries.LoadLibrary( &quot;OOoFBTools&quot; )
	If Not GlobalScope.DialogLibraries.isLibraryLoaded( &quot;OOoFBTools&quot; ) Then
    	GlobalScope.DialogLibraries.LoadLibrary( &quot;OOoFBTools&quot; )
	End If
	oOCREF = CreateUnoDialog( GlobalScope.DialogLibraries.OOoFBTools.OCRErrorFormatingDlg )
	
	&apos; инициализация диалога
	InitOCRErrorFormatingDlg()

	With oOCREF.Model
		.Height	= 65
		.Width	= 219
	End With
	
	&apos; считаем положение диалога из файла настроек
	Dim nX As Integer, nY As Integer
	ReadDlgSettings( getOOoFBToolsSettingsDir() &amp; sOCREFSettings, oOCREF, nX, nY )
	oOCREF.setPosSize( nX, nY, 56, 219, com.sun.star.awt.PosSize.POS )
	VerifyCheckOptions()
	oOCREF.execute()
End Sub

&apos; Кнопка запуска обработки доступна только при выборе хотя бы одной опции корректировки
Sub VerifyCheckOptions()
	If oOCREF.getControl( &quot;ChbBrackets&quot; ).State	= 0 And oOCREF.getControl( &quot;ChbQuotation&quot; ).State = 0 _
			And oOCREF.getControl( &quot;ChbSignPunct&quot; ).State = 0 And oOCREF.getControl( &quot;ChbDash&quot; ).State = 0 Then
		oOCREF.getControl( &quot;cmdbtnWork&quot; ).Enable = False
	Else
		oOCREF.getControl( &quot;cmdbtnWork&quot; ).Enable = True
	End If
End Sub

&apos;******************************************************************
&apos; 						начало обработки
&apos;******************************************************************
Sub OCRErrorFormatWorking()
	&apos; записываем настройки диалога
	SaveSettings( getOOoFBToolsSettingsDir() &amp; sOCREFSettings, oOCREF )
	
	Iter = 1
	Dim ProgressMaxIter As Integer : ProgressMaxIter = 1
	If oOCREF.getControl( &quot;ChbBrackets&quot; ).State		= 1 Then ProgressMaxIter = ProgressMaxIter + 1&apos;4
	If oOCREF.getControl( &quot;ChbQuotation&quot; ).State	= 1 Then ProgressMaxIter = ProgressMaxIter + 1&apos;4
	If oOCREF.getControl( &quot;ChbSignPunct&quot; ).State	= 1 Then ProgressMaxIter = ProgressMaxIter + 1&apos;4
	
	oOCREF.endExecute()
	
	Dim oProgressBar : oProgressBar = ThisComponent.CurrentController.StatusIndicator
	oProgressBar.start( sOCREF.sTitle &amp; &quot;:&quot;, ProgressMaxIter )

	Dim aLeftList()		As String
	Dim aRightList()	As String
	If oOCREF.getControl( &quot;ChbBrackets&quot; ).State = 1 Then
		&apos; обработка курсива и/или bold на скобках в тексте, таблицах, сносках и текстовых врезок (ошибки OCR форматирования
		&apos;TODO!!!!!!!!!!
		&apos; поиск разных видов скобок, и если они есть, то по ним формируем aLeftList() и aRightList()
		&apos; это - для ускорения обработки!!!!!!!
		aLeftList()		= Array( &quot;(&quot;, &quot;{&quot;, &quot;[&quot; )
		aRightList()	= Array( &quot;)&quot;, &quot;}&quot;, &quot;]&quot; )
		TextNoteTableTextFrameWorker( aLeftList(), aRightList(), oProgressBar )
	End If
	
	If oOCREF.getControl( &quot;ChbQuotation&quot; ).State = 1 Then
		&apos; обработка курсива и/или bold на кавычках в тексте, таблицах, сносках и текстовых врезок (ошибки OCR форматирования
		&apos;TODO!!!!!!!!!!
		&apos; поиск разных видов кавычек, и если они есть, то по ним формируем aLeftList() и aRightList()
		&apos; это - для ускорения обработки!!!!!!!
		aLeftList()		= Array( &quot;«&quot; , &quot;“&quot;, &quot;„&quot; )
		aRightList()	= Array( &quot;»&quot; , &quot;”&quot;, &quot;”&quot; )
		TextNoteTableTextFrameWorker( aLeftList(), aRightList(), oProgressBar )
	End If
	
	If oOCREF.getControl( &quot;ChbSignPunct&quot; ).State = 1 Then
		&apos; обработка текста, таблиц, сносок и текстовых врезок: снятие/установка курсива и/или bold со знаков препинания (ошибки OCR форматирования)
		SignPunctWorker( oProgressBar )
	End If

	If oOCREF.getControl( &quot;ChbDash&quot; ).State = 1 Then
		&apos; обработка аттрибутов тире, пробелов вокруг него, и первого символа после него на предмет курсива и полужирного.
		DashAttributeWorking()
	End If

	MsgBox sOCREF.sEndMessage, 64, sOCREF.sTitle
	oProgressBar.End
End Sub

&apos; =========================================================================================================================
&apos; 						Обработка курсива скобок и/или кавычек в тексте, таблицах, сносках и текстовых врезках
&apos; =========================================================================================================================

&apos; обработка текста, таблиц, сносок и текстовых врезок: снятие курсива и/или bold с заданных символов (скобки и кавычки) (ошибки OCR форматирования)
Sub TextNoteTableTextFrameWorker( ByRef aLeftList(), ByRef aRightList(), ByRef oProgressBar As Object )
	Dim oDoc		As Object	: oDoc			= ThisComponent
	Dim vLSymSearch	As Variant	: vLSymSearch	= oDoc.createSearchDescriptor()
	Dim vLSymFound	As Variant

	&apos; левый поисковый символ
	With vLSymSearch
		.SearchRegularExpression = False
		.ValueSearch = False
	End With
	
	Dim i As Integer

	oProgressBar.setValue( Iter ) : Iter = Iter + 1
	For i = 0 To UBound( aLeftList() )
		&apos; левый поисковый символ
		vLSymSearch.SearchString = aLeftList( i )
		&apos; ищем левый поисковый символ
		vLSymFound = oDoc.FindFirst( vLSymSearch )
		If Not IsNull(vLSymFound) Then 
			Do
				&apos; поиск и снятие курсива и/или bold со скобок/кавычек
				UnItalicBoldBracketWorker( vLSymFound, aLeftList( i ), aRightList( i ) )
				&apos; поиск следущего элемента
				vLSymFound = oDoc.FindNext( vLSymFound.End, vLSymSearch )
			Loop Until IsNull( vLSymFound ) &apos; выполняем хотя бы раз пока vLSymFound не пусто
		End If
		
		oProgressBar.setValue( Iter ) : Iter = Iter + 1
	Next i
End Sub
&apos;******************************************************************

&apos; поиск и снятие курсива и/или bold со скобок/кавычек
Sub UnItalicBoldBracketWorker( ByRef vLSymFound As Variant, sFindLeftSymbol As String, sFindRightSymbol As String )
	&apos; нашли левый поисковый символ
	Dim oLSymCurs	As Object : oLSymCurs = vLSymFound.getText().CreateTextCursorByRange( vLSymFound )
	oLSymCurs.collapseToEnd()
		
	&apos; ищем правый поисковый символ
	Dim oRSymCurs	As Object : oRSymCurs = vLSymFound.getText().CreateTextCursorByRange( vLSymFound )
	
	Dim FirstWordLetterAfterRightSymbol As Boolean
	
FindLSym:
	Do While oRSymCurs.goRight(1, True) And ( Not oRSymCurs.Text.compareRegionEnds( oRSymCurs, oRSymCurs.End ) &lt;= 0 )
		FirstWordLetterAfterRightSymbol = False
		Dim symbol As String : symbol = Right( oRSymCurs.getString(), 1 )
		If Right( oRSymCurs.getString(), 1 ) = sFindLeftSymbol Then
			&apos; нашли еще один левый поисковый символ ДО правого поискового символ
			oLSymCurs.gotoRange( oRSymCurs, False )
			oLSymCurs.collapseToEnd()
			GoTo FindLSym
		Else
			If symbol = sFindRightSymbol Then
				&apos; нашли парный правый поисковый символ
				oRSymCurs.goLeft(1, True)
				oRSymCurs.collapseToEnd()
				&apos; ищем 1-й символ после правого символа, кроме пробелоа и/или табуляции
				FirstWordLetterAfterRightSymbol = FindFirstWordLetterAfterRightSymbol( oRSymCurs )
				Exit Do
			Else
				If ( sFindLeftSymbol = &quot;“&quot; OR sFindLeftSymbol = &quot;„&quot; ) And _
					( symbol = sFindRightSymbol Or symbol = &quot;&quot;&quot;&quot; ) Then
					&apos; защита на случай, если вместо &quot;парной&quot; кавычки будет просто &quot;
					&apos; нашли парный правый поисковый символ
					oRSymCurs.goLeft(1, True)
					oRSymCurs.collapseToEnd()
					&apos; ищем 1-й символ после правого символа, кроме пробелоа и/или табуляции
					FirstWordLetterAfterRightSymbol = FindFirstWordLetterAfterRightSymbol( oRSymCurs )
					Exit Do
				End If
			End If
		End If
	Loop

	&apos; ищем последний символ слова левее левого поискового символа, кроме пробелоа и/или табуляции
	Dim EndWordLetterBeforeLeftBracket As Boolean : EndWordLetterBeforeLeftBracket = FindEndWordLetterBeforeLeftSymbol( oLSymCurs )

	&apos; Обработка заданных поисковых символов
	If EndWordLetterBeforeLeftBracket = True And FirstWordLetterAfterRightSymbol = True Then
		UnItalic(  oLSymCurs, oRSymCurs )
	End If
End Sub
&apos;******************************************************************

&apos; ищем последний символ слова левее левого поискового символа, кроме пробелоа и/или табуляции
Function FindEndWordLetterBeforeLeftSymbol( ByRef oLSymCurs As Object ) As Boolean
	FindEndWordLetterBeforeLeftSymbol = False
	Dim s As String
	Do While oLSymCurs.goLeft(1, True)
		s = Left( oLSymCurs.getString(), 1 )
		If s &lt;&gt; &quot;&quot; And s &lt;&gt; Chr(32) And s &lt;&gt; Chr(9) And s &lt;&gt; Chr(160) Then
			If IsLetter( s ) OR IsNumber( s ) Then &apos; нашли последний символ слова левее левой скобки
				FindEndWordLetterBeforeLeftSymbol = True
				Exit Function
			End If
		End If
	Loop
End Function
&apos;******************************************************************

&apos; ищем 1-й символ после поискового символа, кроме пробелоа и/или табуляции
Function FindFirstWordLetterAfterRightSymbol( ByRef oRSymCurs As Object ) As Boolean
	FindFirstWordLetterAfterRightSymbol = False
	Dim s As String
	Do While oRSymCurs.goRight(1, True)
		s = Right( oRSymCurs.getString(), 1 )
		If s &lt;&gt; &quot;&quot; And s &lt;&gt; Chr(32) And s &lt;&gt; Chr(9) And s &lt;&gt; Chr(160) Then
			If IsLetter( s ) OR IsNumber( s ) OR s = &quot;.&quot; OR s = &quot;!&quot; OR s = &quot;?&quot; Then &apos; нашли 1-й символ после правого поискового символа
				FindFirstWordLetterAfterRightSymbol = True
				Exit Function
			End If
		End If
	Loop
End Function
&apos;******************************************************************

&apos; снятие курсива со скобок/кавычек
Function UnItalic( ByRef oLSymCurs As Object, ByRef oRSymCurs As Object ) As Boolean
	UnItalic = False
	Dim IsLItalic As Boolean : IsLItalic = False
	Dim IsRItalic As Boolean : IsRItalic = False
	Dim oCurs As Object : oCurs = oLSymCurs.getText().CreateTextCursorByRange( oLSymCurs )
	oCurs.collapseToStart() : oCurs.goRight(1, True)
	If oCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC Then IsLItalic = True
		
	oCurs.gotoRange( oRSymCurs, False )
	oCurs.collapseToEnd() : oCurs.goLeft(1, True)
	If oCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC Then IsRItalic = True
	If Not IsLItalic And Not IsRItalic Then
		&apos; Если левый символ и правый символ от скобок/кавычек – оба без курсива, то с обоих скобок (кавычек) снимаем курсив.
		oLSymCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		oRSymCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		UnItalic = True
	ElseIf IsLItalic And IsRItalic Then
		&apos; Если левый символ и правый символ от скобок/кавычек – оба с  курсивом, то на обои скобки (кавычки) ставим курсив.
		oLSymCurs.collapseToEnd() : oLSymCurs.goLeft(1, True)
		oLSymCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC
		oRSymCurs.collapseToStart() : oRSymCurs.goRight(1, True)
		oRSymCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC
		UnItalic = True
	Else
		&apos; Если или только левый символ, или только правый символ от скобок/кавычек – с курсивом, а второй - без курсива, то с обоих скобок (кавычек) снимаем курсив.
		oLSymCurs.collapseToEnd() : oLSymCurs.goLeft(1, True)
		oLSymCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		oRSymCurs.goLeft(1, True)
		oRSymCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		UnItalic = True
	End If
End Function

&apos; =========================================================================================================================
&apos; 						Обработка знаков препинания в тексте, таблицах, сносках и текстовых врезках
&apos; =========================================================================================================================
&apos; обработка текста, таблиц, сносок и текстовых врезок: снятие/установка курсива и/или bold со знаков препинания (ошибки OCR форматирования)
Sub SignPunctWorker( ByRef oProgressBar As Object )
	Dim aSignPunctList() As String : aSignPunctList() = Array( &quot;,&quot;, &quot;.&quot;, &quot;:&quot;, &quot;;&quot;, &quot;!&quot;, &quot;?&quot; )
	Dim oDoc As Object : oDoc = ThisComponent
	Dim vSignPunctSearch	As Variant	: vSignPunctSearch	= oDoc.createSearchDescriptor()
	Dim vSignPunctFound		As Variant
	Dim i As Integer
	
&apos;	Dim SrchAttributes(0) As New com.sun.star.beans.PropertyValue
&apos;	SrchAttributes(0).Name = &quot;CharPosture&quot;
	With vSignPunctSearch
		.SearchRegularExpression = False
&apos;		.SetSearchAttributes( SrchAttributes() )
		.ValueSearch			 = False
	End With
		
	oProgressBar.setValue( Iter ) : Iter = Iter + 1
	
	&apos; удаление курсива для знаков препинания в тексте
	For i = 0 To UBound( aSignPunctList() )
		&apos; знак пунктуации с курсивом
		vSignPunctSearch.SearchString = aSignPunctList( i )

		&apos; ищем левый поисковый символ
		vSignPunctFound = oDoc.FindFirst( vSignPunctSearch )
		If Not IsNull(vSignPunctFound) Then 
			Do
				&apos; поиск и обработка курсива на знаках препинания
				UnItalicBoldSignPunctuationWorker( vSignPunctFound, aSignPunctList( i ) )
				&apos; Поиск следущего элемента
				vSignPunctFound = oDoc.FindNext( vSignPunctFound.End, vSignPunctSearch )
			Loop Until IsNull( vSignPunctFound ) &apos; выполняем хотя бы раз пока vSignPunctFound не пусто
		EndIf

		oProgressBar.setValue( Iter ) : Iter = Iter + 1
	Next i
End Sub
&apos;******************************************************************

&apos; поиск и снятие курсива и/или bold с знаков препинания
Sub UnItalicBoldSignPunctuationWorker( ByRef vLSymFound As Variant, WorkSymbol As String )
	Dim oSignPunctCurs As Object : oSignPunctCurs = vLSymFound.getText().CreateTextCursorByRange( vLSymFound )
	
	&apos; Обработка заданных поисковых символов
	Dim IsSoSLetterItalic		As Boolean : IsSoSLetterItalic		= False &apos;1-й символ Начала предложения (для символов , : ;)
	Dim IsSoSLetterBold			As Boolean : IsSoSLetterBold		= False &apos;1-й символ Начала предложения (для символов , : ;)
	Dim IsStartOfWordItalic		As Boolean : IsStartOfWordItalic	= False &apos;1-й символ слова до найденного знака препинания (для символов . ? !)
	Dim IsStartOfWordBold		As Boolean : IsStartOfWordBold		= False &apos;1-й символ слова до найденного знака препинания (для символов . ? !)
		
	If WorkSymbol = &quot;.&quot; Or WorkSymbol = &quot;!&quot; Or WorkSymbol = &quot;?&quot; Then &apos; . ? !
		&apos; ищем 1-й символ Начала предложения (для символов . ? !)
		Dim oStartSentenceLetterCurs As Object : oStartSentenceLetterCurs = oSignPunctCurs.getText().CreateTextCursorByRange( oSignPunctCurs )
		oStartSentenceLetterCurs.gotoStartOfSentence( False ) : oStartSentenceLetterCurs.goRight(1, True)
		If oStartSentenceLetterCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC	Then IsSoSLetterItalic	= True
		If oStartSentenceLetterCurs.CharWeight	= com.sun.star.awt.FontWeight.BOLD	Then IsSoSLetterBold	= True
	
		&apos; работа с курсивом: Если начало предложения — не курсив, то и (. ? !) в конце его — тоже должны быть не курсив.
		If Not IsSoSLetterItalic Then
			oSignPunctCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		Else
			oSignPunctCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC
		End If
		
		&apos; работа с полужирным: Если начало предложения — не bold, то и точка в конце его — тоже должна быть не bold.
		If Not IsSoSLetterBold Then
			oSignPunctCurs.CharWeight = com.sun.star.awt.FontWeight.NORMAL
		Else
			oSignPunctCurs.CharWeight = com.sun.star.awt.FontWeight.BOLD
		End If
	Else &apos;, ; :
		&apos; ищем 1-й символ после найденного знака препинания (для символов , : ;)
		Dim oStartOfWordCurs As Object : oStartOfWordCurs = oSignPunctCurs.getText().CreateTextCursorByRange( oSignPunctCurs )
		oStartOfWordCurs.gotoStartOfWord( False )
		oStartOfWordCurs.collapseToEnd() : oStartOfWordCurs.goRight(1, True)
		If oStartOfWordCurs.CharPosture	= com.sun.star.awt.FontSlant.ITALIC	Then IsStartOfWordItalic	= True
		If oStartOfWordCurs.CharWeight	= com.sun.star.awt.FontWeight.BOLD	Then IsStartOfWordBold		= True
	
		&apos; работа с курсивом:
		If Not IsStartOfWordItalic Then
			oSignPunctCurs.CharPosture = com.sun.star.awt.FontSlant.NONE
		ElseIf IsStartOfWordItalic Then
			oSignPunctCurs.CharPosture = com.sun.star.awt.FontSlant.ITALIC
		End If
		
		&apos; работа с полужирным:
		If Not IsStartOfWordBold Then
			oSignPunctCurs.CharWeight = com.sun.star.awt.FontWeight.NORMAL
		ElseIf IsStartOfWordBold Then
			oSignPunctCurs.CharWeight = com.sun.star.awt.FontWeight.BOLD
		End If
	End If
End Sub

&apos; ===================================================================================
&apos;			локализация Исправления OCR ошибок форматирования
&apos; ===================================================================================
&apos; для хранения сообщений
Type tOCRErrorFormatingData
	sTitle		As String &apos; Исправление OCR ошибок форматирования
	sMess		As String &apos; Выберите хоть одну опцию
	sEndMessage	As String &apos; Обработка документа завершена!
End Type

Public sOCREF As tOCRErrorFormatingData

&apos; =================================================================================================
&apos; инициализация сообщений в зависимости от локали
Sub InitOCRErrorFormatingDataMessage()
	If getOOoLocal() = &quot;ru&quot; OR getOOoLocal() = &quot;uk&quot; Then
		With sOCREF
			.sTitle			= &quot;Исправление OCR ошибок форматирования&quot;
			.sMess			= &quot;Выберите хоть одну опцию&quot;
			.sEndMessage	= &quot;Обработка документа завершена!&quot;
		End With
	Else
		With sOCREF
			.sTitle			= &quot;Fix OCR formatting errors&quot;
			.sMess			= &quot;Select at least one option&quot;
			.sEndMessage	= &quot;Document processing complete!&quot;
		End With
	End If
End Sub

&apos; =================================================================================================
&apos; инициализация диалога в зависимости от локали
Sub InitOCRErrorFormatingDlg()
	&apos; инициализация сообщений в зависимости от локали
	InitOCRErrorFormatingDataMessage()
	
	oOCREF.getModel.Title = sOCREF.sTitle
	
	If getOOoLocal() = &quot;ru&quot; OR getOOoLocal() = &quot;uk&quot; Then
		With oOCREF.getModel
			.ChbBrackets.Label	= &quot;Скобки: (), {}, []&quot;
			.ChbQuotation.Label	= &quot;Кавычки: «», “”, „”&quot;
			.ChbSignPunct.Label	= &quot;Знаки препинания&quot;
			.ChbDash.Label		= &quot;Пробелы вокруг тире, и первый символ после него&quot;
			.cmdbtnWork.Label	= &quot;Обработать&quot;
			.cmdbtnCancel.Label	= &quot;Отмена&quot;
		End With
	Else
		With oOCREF.getModel
			.ChbBrackets.Label	= &quot;Brackets: (), {}, []&quot;
			.ChbQuotation.Label	= &quot;Quotation marks: «», “”, „”&quot;
			.ChbSignPunct.Label	= &quot;Punctuation marksn&quot;
			.ChbDash.Label		= &quot;Spaces around en/em-dash and first character after it&quot;
			.cmdbtnWork.Label	= &quot;Process&quot;
			.cmdbtnCancel.Label	= &quot;Cancel&quot;
		End With
	End If
End Sub

</script:module>